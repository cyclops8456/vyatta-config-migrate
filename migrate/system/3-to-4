#! /usr/bin/perl

# migrate "system ntp-server" to "system ntp server"

use strict;
use XorpConfigParser;
use File::Copy;

my $orig_cfg = shift;
exit 1 unless $orig_cfg;

my $xcp = new XorpConfigParser();
$xcp->parse($orig_cfg);

my $snode = $xcp->get_node(['system']);
exit 0 unless $snode;

my $dst = $xcp->create_node(['system', 'ntp']);
die "Can't make system ntp server\n" unless $dst;

my $children = $snode->{'children'};
exit 0 unless $children;

my @servers;
foreach my $child (@$children) {
    # Find ntp-server entries
    next unless ($child->{'name'} =~ /^ntp-server (.*)$/);
    $child->{'name'} = "server $1";
    push @servers, $child;
}
exit 0 unless @servers;

foreach my $match (@servers) {
    $xcp->move_child($snode, $dst, $match->{'name'});
}

my $tmpname = "/tmp/vyatta_migrate_system.$$";
open (my $tmp, '>', $tmpname)
    or die "Can't open: $tmpname: $!";

select $tmp;
$xcp->output(0);
select STDOUT;
close $tmp;

move($tmpname, $orig_cfg)
    or die "Move $tmpname to $orig_cfg failed: $!";
exit 0;
